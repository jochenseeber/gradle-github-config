
buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        if(!project.hasProperty("bootstrap")) {
            classpath group: "me.seeber.gradle", name: "gradle-bintray-config", version: "1.1.1"
            classpath group: "me.seeber.gradle", name: "gradle-github-config", version: "1.1.1"
            classpath group: "me.seeber.gradle", name: "gradle-license-config", version: "1.1.1"
            classpath group: "me.seeber.gradle", name: "gradle-plugin-publish-config", version: "1.1.1"
            classpath group: "me.seeber.gradle", name: "gradle-project-config", version: "1.1.1"
        }
    }
}

version = "1.1.1"
group = "me.seeber.gradle"
description = "Github plugin"

if(project.hasProperty("bootstrap")) {
    apply plugin: "groovy"
    apply plugin: "maven"
}
else {
    apply plugin: "me.seeber.distribution.bintray"
    apply plugin: "me.seeber.distribution.gradle-plugin"
    apply plugin: "me.seeber.distribution.maven"
    apply plugin: "me.seeber.ide.eclipse"
    apply plugin: "me.seeber.project.gradle-plugin"
    apply plugin: "me.seeber.repository.github"
    apply plugin: "me.seeber.setup.license"
    apply plugin: "me.seeber.validation.checkstyle"

    model {
        projectConfig {
            organization.name = "Jochen Seeber"
            inceptionYear = 2016
            license.id = "BSD 2-Clause"
        }

        githubConfig { user = "jochenseeber" }
    }

    pluginBundle {
        tags = [
            "github",
            "build",
            "configuration"
        ]

        plugins {
            githubRepositoryPlugin {
                id = "me.seeber.repository.github"
                displayName = "Configuration for Github Repositories"
            }
        }
    }
}

dependencies {
    compile group: "me.seeber.gradle", name: "gradle-git-config", version: "1.1.1"
    compile(group: "org.kohsuke", name: "github-api", version: "1.80")

    if(project.hasProperty("bootstrap")) {
        compile(gradleApi())
        compile(localGroovy())

        compileOnly(group: "org.eclipse.jdt", name: "org.eclipse.jdt.annotation", version: "2.0.0")
    }
    else {
        annotations(group: "me.seeber.annotations", name: "jdk-annotations", version: "1.8.0-r.3")
        annotations(group: "me.seeber.annotations", name: "gradle-api-annotations", version: "3.1-r.3")
    }
}

if(project.hasProperty("bootstrap")) {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = "https://plugins.gradle.org/m2" }
        jcenter()
    }
}
